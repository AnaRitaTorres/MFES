class Hokify
instance variables
  public users:set of (User) := {};
  public jobs:set of (Job) := {};

operations
  --Creates a new instance of Hokify
	public Hokify: () ==> Hokify
	Hokify() == return self
	post users = {} and jobs = {};
	
	--Adds a new User
	public createUser: User ==> ()
	createUser(user) == users := users union {user}
	pre user not in set users
	post users = users~ union {user};
	
	--Gets User by Name
	public getUserByName: seq of char ==> User 
	getUserByName(name) == (
		dcl u : User;
		for all user in set users do
			if user.Name = name
			then return user;
			
		return u;
	);
	
	--Adds a new Job
	public addJob: Employer * Job ==> ()
	addJob(user, job) == (
	 user.addJob(job);
	 jobs := jobs union {job};
	)
	pre user in set users and job not in set jobs
	post jobs = jobs~ union {job};
	
	--Delete Job
	public deleteJob: Employer * Job ==> ()
	deleteJob(user, job) == (
		user.deleteJob(job);
		jobs := jobs \ {job}; 
	)
	pre user in set users and job in set jobs
	post jobs = jobs~ \ {job};
	
	--Delete User
	public deleteUser: User ==> ()
	deleteUser(user) == (
		if is_Employer(user)
		then
		for all job in set user.jobs
		do jobs := jobs \ {job};
		
		users := users \ {user};
	);
	
end Hokify
